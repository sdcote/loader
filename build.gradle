buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
  }
}


apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'jacoco'


defaultTasks 'clean', 'build', 'publish', 'fatJar'
group = 'coyote'
version = '0.7.14'

ext {
	title = 'Coyote Loader Framework'
	provider = 'Coyote Systems'
	mainClass = 'coyote.loader.Loader'
}
jar {
    manifest {
		attributes  'Implementation-Title' : "$title",
		'Provider' : "$provider",
		'Main-Class' : "$mainClass",
		'Implementation-Version' : version
	}
}
task fatJar(type: Jar) {
	manifest {
		attributes  'Implementation-Title' : "$title",
		'Provider' : "$provider",
		'Main-Class' : "$mainClass",
		'Implementation-Version' : version
	}
	baseName = project.name + '-all'
	from {
		configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } 
	}{
		exclude "META-INF/*.RSA"
	}
	with jar
}

repositories {
	maven { url "https://dl.bintray.com/sdcote/maven" }
	mavenLocal()
	mavenCentral()
}


ext {
	dataFrameVersion = '1.0.5'
	junitVersion = '4.12'
	httpclientVersion = '4.5.3'
}
dependencies {

	// Abstract Data Type
	compile "coyote:DataFrame:$dataFrameVersion"
	
	// UnitTesting
	testCompile "junit:junit:$junitVersion"	
	testCompile "org.apache.httpcomponents:httpclient:$httpclientVersion"
	testCompile "org.apache.httpcomponents:httpmime:$httpclientVersion"

}



// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}


task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}


// add javadoc/source jar tasks as artifacts
artifacts {
	archives sourcesJar, javadocJar
}


publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			artifact sourcesJar {
				classifier "sources"
			}

			artifact javadocJar {
				classifier "javadoc"
			}
		}
	}
	
	repositories {
		mavenLocal()
	}
}

jacocoTestReport {
   	reports {
       	xml.enabled = true
        html.enabled = true
   	}
}
check.dependsOn jacocoTestReport

task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}

bintray {
	user = bintrayUser
	key = bintrayKey
	publications = ['mavenJava']
	pkg {
		repo = 'maven'
		name = 'Loader'
		desc = 'Component loader for 12+ factor apps, containers and cloud native components.'
		licenses = ['MIT']
		labels = ['Java', 'Component']
		vcsUrl = 'https://github.com/sdcote/loader.git'
	}
}

